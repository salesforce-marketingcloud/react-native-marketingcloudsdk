buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def sdkVersion(){
     def jsonFile = file('../package.json')
     def parsedJson = new groovy.json.JsonSlurper().parseText(jsonFile.text)
     parsedJson.version
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 28)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 28)
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

repositories {
    google()
    maven { url "https://salesforce-marketingcloud.github.io/MarketingCloudSDK-Android/repository" }
    jcenter()
}

dependencies {
    api "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
    api "com.salesforce.marketingcloud:marketingcloudsdk:${safeExtGet('marketingCloudVersion', sdkVersion())}"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'org.mockito:mockito-core:2.28.2'
}
